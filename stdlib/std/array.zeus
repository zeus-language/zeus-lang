use core::libc;
use core::utils;
/**
* A dynamic array implementation in Zeus
* @tparam T The type of elements stored in the array
*/
struct array<T>{
    length : i64,
    capacity : i64,
    data : *T,

    pub fn length(self: &array<T>) : i64 {
        return self.length;
    }

    pub fn push(self: mut &array<T>,value : T) : void {
        let size_of_t : i64 = sizeof<T>();
        if self.length >= self.capacity {
            // Resize logic (simple doubling strategy)
            let mut new_capacity : i64;
            if self.capacity == 0 as i64 {
                new_capacity = 1 as i64;
            }
            else {
                new_capacity = self.capacity * 2 as i64;
            }
            self.data = realloc(self.data as *u8, new_capacity as u64 * size_of_t as u64) as *T;

            self.capacity = new_capacity;
        }
        self.data[self.length] = value;
        self.length = self.length + 1 as i64;
    }

//    fn [](index : i32) : T {
//        if index < 0 or index >= length {
//            panic("Index out of bounds");
//        }
//        return data[index];
//    }

    pub fn set(self: mut &array<T>,index : i64, value : T) : void {
        if index < 0 as i64 or index >= self.length {
            panic("Index out of bounds");
        }
        self.data[index ] = value;
    }

    pub fn pop(self: mut &array<T>) : T {
        if self.length == 0 as i64 {
            panic("Pop from empty array");
        }
        self.length = self.length - 1 as i64;
        return self.data[self.length];
    }
    pub fn get(self: &array<T>,index : i64) : T {
        if index < 0 as i64 or index >= self.length {
            panic("Index out of bounds");
        }

        let value: T = self.data[index];
        return value;
    }

    pub fn clear(self: mut &array<T>) : void {
        self.length = 0 as i64;
    }

    pub fn destroy(self: mut &array<T>) : void {
        core::libc::free(self.data as *u8);
        self.data = null as *T;
        self.length = 0 as i64;
        self.capacity = 0 as i64;
        self = null as &array<T>;
    }
}

pub fn new_array<T>() : array<T> {
    return array<T> { data: null, length: 0, capacity: 0 };
}